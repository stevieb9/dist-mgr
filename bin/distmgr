#!/usr/bin/env perl
use warnings;
use strict;
# dist-mgr/bin/distmgr create -m Test::Module -a Steve Bertrand -e steveb@cpan.org -u stevieb9 -r test-module -V
use Cwd qw(getcwd);
use Data::Dumper;
use Dist::Mgr qw(:all);
use Getopt::Long qw(:config no_ignore_case);

my $cmd = _init();    # shifts @ARGV

my %args;
my $git = 1;

GetOptions(
    "h|help"      => \$args{help},
    "m|module=s@" => \$args{modules},
    "a|author=s"  => \$args{author},
    "e|email=s"   => \$args{email},
    "r|repo=s"    => \$args{repo},
    "u|user=s"    => \$args{user},
    "v|version=s" => \$args{version},
    "V|verbose"   => \$args{verbose},

    "g|gitignore"   => \$args{install_gitignore},
    "c|ci"          => \$args{install_ci},
    "B|badges"      => \$args{add_badges},
    "b|bugtracker"  => \$args{add_bugtracker},
    "R|repository"  => \$args{add_repository},
    "A|all"         => \$args{install_all},
);

commands()->{$cmd}(%args);

# Commands

sub build {
    my (%args) = @_;

    for (qw(modules author email)) {
        if (! exists $args{$_}) {
            warn "create: requires '$_' argument\n";
            help();
        }
    }
    for (qw(repo user)) {
        if (! exists $args{$_} || ! defined $args{$_}) {
            warn "Git functionality disabled... need both 'repo' and 'user'\n\n";
            $git = 0;
            last;
        }
    }

    my $module = $args{modules}->[0];
    my $dir;

    # Clone the repository, or create the directory if no VCS
    if ($git) {
        git_clone($args{user}, $args{repo}, $args{verbose});
        $dir = $args{repo};
    }
    else {
        $dir = $module;
        $dir =~ s/::/-/g;
        mkdir $dir or die "Can't create the '$dir' directory: $!";
    }

    # Change into module directory
    chdir $dir or die "Can't change into the '$dir' directory: $!";

    # Create the distribution
    init(%args);

    # Move the files to the module dir
    move_distribution_files($module);

    # Remove our unwanted files
    remove_unwanted_files();

    # Changes
    changes($module);

    # MANIFEST.SKIP
    manifest_skip();

    # manifest.t
    manifest_t();

    # Git actions
    if ($git) {
        # .gitignore
        git_ignore();

        # CI config
        ci_github();

        # CI/Coverage badges
        ci_badges($args{user}, $args{repo});

        # Add bugtracker to Makefile.PL
        add_bugtracker($args{user}, $args{repo});

        # Add repository to Makefile.PL
        add_repository($args{user}, $args{repo});

        # git add
        git_add($args{verbose});

        # git commit
        git_commit("Initial import of $module", $args{verbose});

        # git push
        git_push($args{verbose});
    }
}
sub cycle {
}
sub dist {
    my (%args) = @_;

    for (qw(modules author email)) {
        if (! exists $args{$_}) {
            warn "create: requires '$_' argument\n";
            help();
        }
    }

    my $module = $args{modules}->[0];
    my $dir = $module;
    $dir =~ s/::/-/g;
    mkdir $dir or die "Can't create the '$dir' directory: $!";

    # Change into module directory
    chdir $dir or die "Can't change into the '$dir' directory: $!";

    # Create the distribution
    init(%args);

    # Move the files to the module dir
    move_distribution_files($module);

    # Remove our unwanted files
    remove_unwanted_files();

    # Changes
    changes($module);

    # MANIFEST.SKIP
    manifest_skip();

    # manifest.t
    manifest_t();
}
sub install {
    my (%args) = @_;

    git_ignore() if $args{install_gitignore} || $args{install_all};
    ci_github()  if $args{install_ci}        || $args{install_all};

    if (! defined $args{user} || ! defined $args{repo}) {
        if ($args{add_badges}) {
            die "--badges requires --user and --repo\n";
        }
        if ($args{add_bugtracker}) {
            die "--bugtracker requires --user and --repo\n";
        }
        if ($args{add_repository}) {
            die "--repository requires --user and --repo\n";
        }
        if ($args{install_all}) {
            die "badges, bugtracker and repository require --user and --repo\n";
        }
    }

    ci_badges($args{user}, $args{repo})      if $args{add_badges}        || $args{install_all};
    add_bugtracker($args{user}, $args{repo}) if $args{add_bugtracker}    || $args{install_all};
    add_repository($args{user}, $args{repo}) if $args{add_repository}    || $args{install_all};
}
sub release {
    my (%args) = @_;

    die "release command requires 'module' argument" if ! defined $args{modules};

    my $module = $args{modules}->[0];

    for (qw(repo user)) {
        if (! exists $args{$_} || ! defined $args{$_}) {
            warn "Git functionality disabled... need both 'repo' and 'user'\n\n";
            $git = 0;
            last;
        }
    }

    # Changes - Add release date
    changes_date();

    # Git release (commit, push, CI)
    my $module_file = $module;
    $module_file =~ s/::/\//g;
    $module_file = 'lib/' . $module_file . '.pm';

    my $ver;
    if (! eval { $ver = version_info($module_file)->{$module_file}; 1 }) {
        die "The $module_file file for $module can't be found\n";
    }
    my $ver_incr = version_incr($ver);
    print "$ver: $ver_incr\n";
#    git_release($ver)
}

# Functional

sub help {
    my @data = <DATA>;
    print $_ for @data;
    exit;
}
sub commands {
    return {
        create  => \&build,
        cycle   => \&cycle,
        dist    => \&dist,
        install => \&install,
        release => \&release,
    };
}

# Private

sub _init {
    help() if ! @ARGV;
    my $command = shift @ARGV;
    help() if ! defined commands()->{$command};
    return $command;
}
sub _check_repo_info {
    my (%args) = @_;
}

__DATA__
Usage: distmgr <command> [OPTIONS]

Commands:

create - Creates a full blown distribution with everything included


    -m | --module   Mandatory: The module name (eg. Test::Module)
    -a | --author   Mandatory: The name of the author (eg. Steve Bertrand)
    -e | --email    Mandatory: The email address of the author
    -u | --user     Optional:  The Github username (eg. stevieb9)
    -r | --repo     Optional:  The Github repository name (eg. test-module)
    -V | --verbose  Optional:  Display verbose output for each process

Notes: We will skip adding repository information, skip adding CI badge info,
and skip adding repository and bugtracker information to C<Makefile.PL> file
if --user or --repo are not present in the parameter list.

cycle
    -v| --version

dist - Creates a bare-bones distribution with just the basics

    -m | --module   Mandatory: The module name (eg. Test::Module)
    -a | --author   Mandatory: The name of the author (eg. Steve Bertrand)
    -e | --email    Mandatory: The email address of the author
    -V | --verbose  Optional:  Display verbose output for each process

install - Installs/adds various files or file sections

    -g | --gitignore    Install .gitignore file
    -c | --ci           Install Github Actions CI configuration file
    -B | --badges       Insert CI/Coverage badges links into the module's POD
    -b | --bugtracker   Insert bugtracker information into Makefile.PL
    -R | --repository   Insert repository information into Makefile.PL
    -A | --all          Insert/Install all above options

Notes: For badges, bugtracker and repository, the --user and --repo arguments
must be supplied.

release
    -v

